% num(N) - available symbols and implicit square dimension

num(1..n).

% for each block (R,C), either there is a queen or there is not
queen(R,C) :- not no_queen(R,C), num(R;C).
no_queen(R,C) :- not queen(R,C), num(R;C).

% if there is a queen in row R, row R is occupied
occupied(R) :- queen(R,C), num(R;C).

% every row must be occupied
:- not occupied(R), num(R).

% no two queens in the same row
:- num(R;C;C1), C!=C1, queen(R,C), queen(R,C1).

% no two queens in the same column
:- num(R;R1;C), R!=R1, queen(R,C), queen(R1,C).

% no two queens in the same diagonal
:- num(R;R1;C;C1), R!=R1, queen(R,C), queen(R1,C1),
   abs(R-R1)==abs(C-C1).
